options {
    MULTI=true;
	NODE_EXTENDS="wand.core.WandNode";
	NODE_PACKAGE="wand.parser";
	VISITOR=true;
}


/* *** Specification of the parser class *** */

PARSER_BEGIN(WandParser)
package wand.parser;

public class WandParser {}
PARSER_END(WandParser)

/* *** Token specification *** */

/* Skip whitespace */
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}

/* Reserved words */
TOKEN: {
	< VOID: "void">
|	< INT: "int">
|	< FLOAT: "float">
}

/* Literals */
TOKEN: {
  < FLOAT_LITERAL: (["0"-"9"])+ "." (["0"-"9"])+ >    // Floating point literal
| < INTEGER_LITERAL:
      <DECIMAL_LITERAL> (["l","L"])?
    | <HEX_LITERAL> (["l","L"])?
    | <OCTAL_LITERAL> (["l","L"])?
    | <BINARY_LITERAL> (["l","L"])?
  >

| < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
| < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
| < #BINARY_LITERAL: "0" ["b","B"] (["0"-"1"])+ >
| < #OCTAL_LITERAL: "0" (["0"-"7"])* >

| "null"
| "true"
| "false"
}

/* Operators */
TOKEN: {
  < LPAREN: "(" >
| < RPAREN: ")" >
| < OPENBLOCK: "{" >
| < CLOSEBLOCK: "}" >

| < OP_INC: "++" >
| < OP_DEC: "--" >

| < OP_ADD_ASSIGN: "+=" >
| < OP_SUB_ASSIGN: "-=" >

| < OP_MUL_ASSIGN: "*=" >
| < OP_DIV_ASSIGN: "/=" >
| < OP_MOD_ASSIGN: "%=" >

| < OP_EQUALS: "==" >
| < OP_NOT_EQUAL: "!=" >

| < OP_CMP_LTE: "<=" >
| < OP_CMP_LT: "<" >
| < OP_CMP_GTE: ">=" >
| < OP_CMP_GT: ">" >

| < OP_NOT_IN: "!in" >
| < OP_IN: "in" >

| < OP_ADD: "+" >
| < OP_SUB: "-" >

| < OP_MUL: "*" >
| < OP_DIV: "/" >
| < OP_MOD: "%" >

| < OP_ASSIGN: "=" >

| < OP_LOGICAL_OR: "||" >
| < OP_BITWISE_OR: "|" >

| < MODIFIER_PUBLIC: "public" >
| < MODIFIER_PRIVATE: "private" >
| < MODIFIER_PROTECTED: "protected" >
| < MODIFIER_STATIC: "static" >
| < MODIFIER_ABSTRACT: "abstract" >
| < MODIFIER_FINAL: "final" >
| < MODIFIER_COMPAT: "compat" >

| < MODIFIER_ARG: ":" >

| < STMT_RETURN: "return" >
| < STMT_ASSERT: "assert" >

| < STMT_IF: "if" >
| < STMT_ELSE: "else" >
| < STMT_DO: "do" >
| < STMT_WHILE: "while" >
| < STMT_FOR: "for" >
| < STMT_FOREACH: "foreach" >

| < END_STMT: ";" >

}

/* Identifiers */
TOKEN: {
  < ID: (["A"-"Z", "a"-"z", "_"])(["A"-"Z", "a"-"z", "_", "0"-"9"])* >
}

/* *** Context-free grammar (EBNF) *** */

/* The start nonterminal and its productions. */

ASTProgram Program() : {
    ASTDeclarations declarations;
}
{
    Declarations()
    
    { return jjtThis; }
}

/* Other nonterminals and their productions */

// Declarations -> Declaration [Declarations]
void Declarations() : {
    ASTDeclaration declaration;
}
{
    ( Declaration() )*
}



// Declaration -> FunctionDeclaration
void Declaration() #void : {}
{
	DecoratedFunctionDeclaration()
}

void DecoratedFunctionDeclaration() : {}
{
    Modifiers() FunctionDeclaration()
}

void Modifiers() : {}
{
    (Modifier())*
}

void Modifier() : {}
{
    "public" |
    "private" |
    "protected" |
    "static" |
    "abstract" |
    "final" |
    "compat" ":" <ID>
}

// FunctionDeclaration -> "void" Identifier "(" ")" "{" FunctionBody "}"
void FunctionDeclaration() : {
	Token functionName;
}
{
	(
	    Type() functionName=<ID> FunctionParameters() BlockStatement()
	    { jjtThis.setFunctionName( functionName.image ); }
	)
}

void FunctionParameters() : {}
{
    <LPAREN> [FunctionParameter() ("," FunctionParameter())*] <RPAREN>
}

void FunctionParameter() : {}
{
    Type() Identifier() ["=" VariableInitializer()]
}

// FunctionBody -> FunctionDeclaration
/*ASTFunctionBody FunctionBody() : {}
{
    { return jjtThis; }
}*/

// Type ->
void Type() #void : {}
{
    LOOKAHEAD(2)
    ArrayType() |
	BuiltinType()
}

void BuiltinType() : {
    Token t;
}
{
	(
	    t="void" | 
	    t="int" | 
	    t="float" | 
	    t=<ID>
	)
	
	{ jjtThis.setTypeName( t.image ); }
}

void ArrayType() : {}
{
	BuiltinType() ("[" "]")+
}





void QualifiedIdentifier() #QualifiedIdentifier(>1) : {}
{
    (
        Identifier() ("." <ID>)*
    )
}

void Literal() #void : {}
{
    (
        IntegerLiteral() |
        FloatingPointLiteral() |
        /*CharacterLiteral() |
        StringLiteral() |*/
        BooleanLiteral() |
        NullLiteral()
    )
}

void Expression() #void : {}
{
    AssignExpression()
}

void AssignExpression() #AssignExpression(>1) : {}
{
    // was: InfixExpression() [AssignOperator() InfixExpression()]
    // but this allowed syntax "3 += 1" which would be invalid and need
    // to be picked up elsewhere
    LOOKAHEAD(2)
    Identifier() AssignOperator() AssignExpression() |
    ConditionalExpression()
}

void ConditionalExpression() #ConditionalExpression(>1) : {}
{
    OrOrExpression() ["?" Expression() ":" ConditionalExpression()]
}

void OrOrExpression() #InfixExpression(>1) : {}
{
    AndAndExpression() (OrOrOperator() AndAndExpression())*
}

void AndAndExpression() #InfixExpression(>1) : {}
{
    OrExpression() (AndAndOperator() OrExpression())*
}

void OrExpression() #InfixExpression(>1) : {}
{
    XorExpression() (OrOperator() XorExpression())*
}

void XorExpression() #InfixExpression(>1) : {}
{
    AndExpression() (XorOperator() AndExpression())*
}

void AndExpression() #InfixExpression(>1) : {}
{
    CmpExpression() (AndOperator() CmpExpression())*
}

void CmpExpression() #InfixExpression(>1) : {}
{
    ShiftExpression() [EqualExpressionRest() | RelExpressionRest() | InExpressionRest()]
}

void ShiftExpression() #InfixExpression(>1) : {}
{
    AddExpression() (ShiftOperator() AddExpression())*
}

void EqualExpressionRest() #void : {}
{
    EqualOperator() ShiftExpression()
}

void RelExpressionRest() #void : {}
{
    RelOperator() ShiftExpression()
}

void InExpressionRest() #void: {}
{
    InOperator() ShiftExpression()
}

void CatExpression() #InfixExpression(>1) : {}
{
    MulExpression() CatOperator() MulExpression()
}

void AddExpression() #InfixExpression(>1) : {}
{
    MulExpression() (AddOperator() MulExpression())*
}

void MulExpression() #InfixExpression(>1) : {}
{
    PowExpression() (MulInfixOperator() PowExpression())*
}

void PowExpression() #InfixExpression(>1) : {}
{
    UnaryExpression() [PowOperator() PowExpression()]
}

void UnaryExpression() #UnaryExpression(>1) : {}
{
    PostfixExpression() |
    UnaryOperator() UnaryExpression()
}

void PostfixExpression() #PostfixExpression(>1) : {}
{
    PrimaryExpression() (PostfixOperator())*
}

/*
void InfixExpression() #InfixExpression(>1) : {}
{
    MulInfixExpression() (InfixOperator() InfixExpression())*
}

void MulInfixExpression() #InfixExpression(>1) : {}
{
    PrefixExpression() [MulInfixOperator() MulInfixExpression()]
}

void PrefixExpression() #PrefixExpression(>1) : {}
{
    PrefixOperator() PrefixExpression() |
    PostfixExpression()
}

void PostfixExpression() #PostfixExpression(>1) : {}
{
    PrimaryExpression() (PostfixOperator())*
    
}*/

void PrimaryExpression() #void : {}
{
    <LPAREN> Expression() <RPAREN> |
    Literal() |
    FunctionCall()
}

void FunctionCall() #FunctionCall(>1) : {}
{
    QualifiedIdentifier() [Arguments()]
}

void Arguments() : {}
{
    <LPAREN> [Expression()] ("," Expression())* <RPAREN>
}


void AssignOperator() : {
    Token t;
}
{
    (
        t="=" |
        t="+=" |
        t="-=" |
        t="*=" |
        t="/=" |
        t="&=" |
        t="|=" |
        t="^=" |
        t="%=" |
        t="<<=" |
        t=">>=" |
        t=">>>="
    )
    
    { jjtThis.setTokenType( t.kind ); }
}

void AddOperator() #InfixOperator : {
    Token t;
}
{
    (
        t="+" |
        t="-"
    )
    
    { jjtThis.setTokenType( t.kind ); }
}

void MulInfixOperator() #InfixOperator : {
    Token t;
}
{
    (
        t="*" |
        t="/" |
        t="%"
    )
    
    { jjtThis.setTokenType( t.kind ); }
}

void OrOrOperator() #InfixOperator : {
    Token t;
}
{
    t="||"
    
    { jjtThis.setTokenType( t.kind ); }
}

void AndAndOperator() #InfixOperator : {
    Token t;
}
{
    t="&&"
    
    { jjtThis.setTokenType( t.kind ); }
}

void OrOperator() #InfixOperator : {
    Token t;
}
{
    t="|"
    
    { jjtThis.setTokenType( t.kind ); }
}

void XorOperator() #InfixOperator : {
    Token t;
}
{
    t="^"
    
    { jjtThis.setTokenType( t.kind ); }
}

void AndOperator() #InfixOperator : {
    Token t;
}
{
    t="&"
    
    { jjtThis.setTokenType( t.kind ); }
}

void ShiftOperator() #InfixOperator : {
    Token t;
}
{
    (
        t=">>>" |
        t=">>" |
        t="<<"
    )
    
    { jjtThis.setTokenType( t.kind ); }
}

void EqualOperator() #InfixOperator : {
    Token t;
}
{
    (
        t="==" |
        t="!="
    )
    
    { jjtThis.setTokenType( t.kind ); }
}

void RelOperator() #InfixOperator : {
    Token t;
}
{
    (
        t="<" |
        t="<=" |
        t=">" |
        t=">="
    )
    
    { jjtThis.setTokenType( t.kind ); }
}

void InOperator() #InfixOperator : {
    Token t;
}
{
    (
        t="in" |
        t="!in"
    )
    
    { jjtThis.setTokenType( t.kind ); }
}

void CatOperator() #InfixOperator : {
    Token t;
}
{
    (
        t="~"
    )
    
    { jjtThis.setTokenType( t.kind ); }
}

void PowOperator() #InfixOperator : {
    Token t;
}
{
    (
        t="^^"
    )
    
    { jjtThis.setTokenType( t.kind ); }
}

void UnaryOperator() : {}
{
    "&" |
    "++" |
    "--" |
    "*" |
    "-" |
    "+" |
    "!" |
    "~"
}

void PostfixOperator() : {}
{
    "++" | "--"
}



void IntegerLiteral() : {
    Token t;
}
{
    t=<INTEGER_LITERAL>
    { jjtThis.fromString( t.image ); }
}

void FloatingPointLiteral() : {}
{
    <FLOAT_LITERAL>
}
/*
void CharacterLiteral() : {}
{
    <FPLIT>
}

void StringLiteral() : {}
{
    <FPLIT>
}
*/
void BooleanLiteral() : {}
{
    "true" | "false"
}

void NullLiteral() : {}
{
    "null"
}


/*
void Block() : {}
{
    "{" BlockStatements() "}"
}

void BlockStatements() #void : {}
{
    (BlockStatement())*
}

void BlockStatement() #void : {}
{
    LOOKAHEAD(2)
    LocalVariableDeclarationStatement() |
    Statement()
}
*/

void LocalVariableDeclarationStatement() : {}
{
    Type() VariableDeclarators() ";"
}

void Statement() #void : {}
{
    ";" |
    NonEmptyStatement() |
    ScopeBlockStatement()
}

void ScopeBlockStatement() #void : {}
{
    BlockStatement()
}

void BlockStatement() : {}
{
    "{" (Statement())* "}"
}

void IfStatement() : {}
{
    "if" "(" IfCondition() ")" ThenStatement() ["else" ElseStatement()]
}

void IfCondition() #void : {}
{
    Expression()
}

void ThenStatement() #void : {}
{
    ScopeStatement()
}

void ElseStatement() #void : {}
{
    ScopeStatement()
}

void ScopeStatement() #void : {}
{
    NonEmptyStatement() |
    BlockStatement()
}

void NonEmptyStatement() #void : {}
{
    LOOKAHEAD(2)
    ExpressionStatement() |
    ReturnStatement() |
    AssertStatement() |
    IfStatement() |
    LocalVariableDeclarationStatement()
}

void AssertStatement() : {}
{
    "assert" Expression() [":" Expression()] ";"
}

void ReturnStatement() : {}
{
    "return" Expression() ";"
}

void ExpressionStatement() : {}
{
    Expression() ";"
}

void VariableDeclarators() #void : {}
{
    VariableDeclarator() ("," VariableDeclarator())*
}

void VariableDeclarator() : {}
{
    Identifier() ["=" VariableInitializer()]
}

void VariableInitializer() #void : {}
{
    Expression()
    // | ArrayInitializer()
}



void Identifier() : {
    Token t;
}
{
    t=<ID> { jjtThis.setIdentifier( t.image ); }
}
