/*

Task: Make this parser implement a basic Wand language instead

Program -> Declarations

Declarations -> Declaration [Declarations]

Declaration -> FunctionDeclaration

FunctionDeclaration -> "void" Identifier "(" ")" "{" FunctionBody "}"

FunctionBody -> FunctionDeclaration


*/



/* This example shows how to specify a simple parser for a toy calculator
   language with floats, multiplication, and let-expressions.
   Here is an example program in this language:
   
      let radius = 5.0 in
        let pi = 3.14 in
          2.0 * pi * radius 

*/

options {
    MULTI=true;
	NODE_EXTENDS="wand.core.WandNode";
	NODE_PACKAGE="wand.parser";
}


/* *** Specification of the parser class *** */

PARSER_BEGIN(WandParser)
package wand.parser;

public class WandParser {}
PARSER_END(WandParser)

/* *** Token specification *** */

/* Skip whitespace */
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}

/* Reserved words */
TOKEN: {
	< VOID: "void">
|	< INT: "int">
|	< FLOAT: "float">
}

/* Literals */
TOKEN: {
  < FLOAT_LITERAL: (["0"-"9"])+ "." (["0"-"9"])+ >    // Floating point literal
| < INTEGER_LITERAL:
      <DECIMAL_LITERAL> (["l","L"])?
    | <HEX_LITERAL> (["l","L"])?
    | <OCTAL_LITERAL> (["l","L"])?
    | <BINARY_LITERAL> (["l","L"])?
  >

| < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
| < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
| < #BINARY_LITERAL: "0" ["b","B"] (["0"-"1"])+ >
| < #OCTAL_LITERAL: "0" (["0"-"7"])* >

| "null"
| "true"
| "false"
}

/* Operators */
TOKEN: {
  < LPAREN: "(" >
| < RPAREN: ")" >
| < OPENBLOCK: "{" >
| < CLOSEBLOCK: "}" >

| < OPINC: "++" >
| < OPDEC: "--" >

| < OPADD: "+" >
| < OPSUB: "-" >

| < OPASSIGN: "=" >

}

/* Identifiers */
TOKEN: {
  < ID: (["A"-"Z", "a"-"z", "_"])(["A"-"Z", "a"-"z", "_", "0"-"9"])* >
}

/* *** Context-free grammar (EBNF) *** */

/* The start nonterminal and its productions. */

ASTProgram Program() : {
    ASTDeclarations declarations;
}
{
    Declarations()
    
    { return jjtThis; }
}

/* Other nonterminals and their productions */

// Declarations -> Declaration [Declarations]
void Declarations() : {
    ASTDeclaration declaration;
}
{
    ( Declaration() )*
}



// Declaration -> FunctionDeclaration
void Declaration() #void : {}
{
	FunctionDeclaration()
}

// FunctionDeclaration -> "void" Identifier "(" ")" "{" FunctionBody "}"
void FunctionDeclaration() : {
	Token functionName;
}
{
	(
	    Type() functionName=<ID> <LPAREN> <RPAREN> Block()
	    { jjtThis.setFunctionName( functionName.image ); }
	)
}

// FunctionBody -> FunctionDeclaration
/*ASTFunctionBody FunctionBody() : {}
{
    { return jjtThis; }
}*/

// Type ->
void Type() #void : {}
{
    LOOKAHEAD(2)
    ArrayType() |
	BuiltinType()
}

void BuiltinType() : {
    Token t;
}
{
	(
	    t="void" | 
	    t="int" | 
	    t="float" | 
	    t=<ID>
	)
	
	{ jjtThis.setTypeName( t.image ); }
}

void ArrayType() : {}
{
	BuiltinType() ("[" "]")+
}





void QualifiedIdentifier() : {}
{
    (
        <ID> [ "." QualifiedIdentifier() ]
    )
}

void Literal() #void : {}
{
    (
        IntegerLiteral() |
        FloatingPointLiteral() |
        /*CharacterLiteral() |
        StringLiteral() |*/
        BooleanLiteral() |
        NullLiteral()
    )
}

void Expression() : {}
{
    AssignExpression()
}

void AssignExpression() #AssignExpression(>1) : {}
{
    // was: InfixExpression() [AssignOperator() InfixExpression()]
    // but this allowed syntax "3 += 1" which would be invalid and need
    // to be picked up elsewhere
    [Identifier() AssignOperator()] InfixExpression()
}

void InfixExpression() #InfixExpression(>1) : {}
{
    PrefixExpression() [InfixOperator() PrefixExpression()]
}

void PrefixExpression() #PrefixExpression(>1) : {}
{
    PrefixOperator() PrefixExpression() |
    PostfixExpression()
}

void PostfixExpression() #PostfixExpression(>1) : {}
{
    Primary() (PostfixOperator())*
    
}

void Primary() #void : {}
{
    <LPAREN> Expression() <RPAREN> |
    Literal()
}



void AssignOperator() : {
    Token t;
}
{
    (
        t="=" |
        t="+=" |
        t="-=" |
        t="*=" |
        t="/=" |
        t="&=" |
        t="|=" |
        t="^=" |
        t="%=" |
        t="<<=" |
        t=">>=" |
        t=">>>="
    )
    
    { jjtThis.setTokenType( t.kind ); }
}

void InfixOperator() : {}
{
    "||" |
    "&&" |
    "|" |
    "^" |
    "&" |
    "==" |
    "!=" |
    "<" |
    ">" |
    "<=" |
    ">=" |
    "<<" |
    ">>" |
    ">>>" |
    "+" |
    "-" |
    "*" |
    "/" |
    "%"
}

void PrefixOperator() : {}
{
    "++" |
    "--" |
    "!" |
    "~" |
    "+" |
    "-"
}

void PostfixOperator() : {}
{
    "++" | "--"
}



void IntegerLiteral() : {}
{
    <INTEGER_LITERAL>
}

void FloatingPointLiteral() : {}
{
    <FLOAT_LITERAL>
}
/*
void CharacterLiteral() : {}
{
    <FPLIT>
}

void StringLiteral() : {}
{
    <FPLIT>
}
*/
void BooleanLiteral() : {}
{
    "true" | "false"
}

void NullLiteral() : {}
{
    "null"
}



void Block() : {}
{
    "{" BlockStatements() "}"
}

void BlockStatements() #void : {}
{
    (BlockStatement())*
}

void BlockStatement() #void : {}
{
    LOOKAHEAD(2)
    LocalVariableDeclarationStatement() |
    Statement()
}

void LocalVariableDeclarationStatement() : {}
{
    Type() VariableDeclarators() ";"
}

void Statement() #void : {}
{
    Block() |
    ExpressionStatement()
}

void ExpressionStatement() : {}
{
    Expression() ";"
}

void VariableDeclarators() #void : {}
{
    VariableDeclarator() ("," VariableDeclarator())*
}

void VariableDeclarator() : {}
{
    Identifier() ["=" VariableInitializer()]
}

void VariableInitializer() #void : {}
{
    Expression()
    // | ArrayInitializer()
}



void Identifier() : {
    Token t;
}
{
    t=<ID> { jjtThis.setIdentifier( t.image ); }
}